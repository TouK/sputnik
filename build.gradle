apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'coveralls'

apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.7
targetCompatibility = 1.7
group = 'pl.touk'
version = '1.4.0'
mainClassName = 'pl.touk.sputnik.Main'

repositories {
    mavenCentral()
}

//noinspection GroovyAssignabilityCheck
dependencies {
    compile 'org.apache.httpcomponents:httpclient:4.3.1'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.3.0'
    compile 'com.intellij:annotations:12.0'
    compile 'org.apache.commons:commons-lang3:3.1'
    compile 'org.apache.commons:commons-io:1.3.2'
    compile 'org.slf4j:slf4j-api:1.7.5'
    compile 'org.slf4j:log4j-over-slf4j:1.7.7'
    compile 'ch.qos.logback:logback-classic:1.0.13'
    compile 'org.projectlombok:lombok:1.12.6'
    compile 'commons-cli:commons-cli:1.2'

    compile 'com.jayway.jsonpath:json-path:0.9.1'

    // Checkstyle dependencies
    compile 'com.puppycrawl.tools:checkstyle:6.1'

    // PMD dependencies
    compile('net.sourceforge.pmd:pmd-dist:5.2.1') {
        exclude group: 'jaxen'
        exclude group: 'xerces'
        exclude group: 'junit'
        exclude group: 'org.apache.ant'
        exclude group: 'org.ow2.asm'
    }

    compile('jaxen:jaxen:1.1.6') { //1.1.6 in FindBugs
        exclude group: 'xerces'
        exclude group: 'xalan'
        exclude group: 'com.ibm.icu'
    }
//    compile 'org.ow2.asm:asm:5.0_BETA' // conflicting with FindBugs
    compile('xerces:xercesImpl:2.9.1') {
        exclude group: 'xml-apis'
    }

    // FindBugs dependencies
    compile('com.google.code.findbugs:findbugs:3.0.0') {
//        exclude group: 'xml-apis'
//        exclude group: 'jaxen'
//        exclude group: 'asm'
    }
    compile 'xml-apis:xml-apis:1.0.b2' // this version is needed by FindBugs on runtime
//    runtime 'asm:asm-commons:3.3'

    // Scalastyle http://www.scalastyle.org/
    compile 'org.scalastyle:scalastyle_2.10:0.4.0'

    // CodeNarc http://codenarc.sourceforge.net/
    compile('org.codenarc:CodeNarc:0.21') {
        exclude module: 'groovy'
    }

    compile 'org.codehaus.groovy:groovy:2.3.4'

    // JSLint
    compile 'com.googlecode.jslint4java:jslint4java:2.0.5'

    // JSHint
    compile 'pl.gildur:jshint4j:1.0.0'

    // Test dependencies
    testCompile 'junit:junit:4.11'
    testCompile 'org.mockito:mockito-all:1.9.5'
    testCompile 'org.assertj:assertj-core:1.5.0'
    testCompile 'com.googlecode.catch-exception:catch-exception:1.2.0'
    testCompile('com.github.tomakehurst:wiremock:1.46') {
        exclude group: 'log4j'
    }
}

// Jacoco + coveralls
buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:0.3.0'
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

// quick way to copy all project dependencies (jar files) into one directory
// similar to dependency:copy-dependencies
task copyToLib(type: Copy) {
    into "$buildDir/lib"
    from configurations.runtime
}


artifacts {
    archives jar

    archives javadocJar
    archives sourcesJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'Sputnik'
                packaging 'jar'
                description 'Static code review for your Gerrit and Stash patchsets. Runs Checkstyle, PMD and FindBugs for you!'
                url 'https://github.com/TouK/sputnik/'

                scm {
                    url 'scm:git@github.com:TouK/sputnik.git'
                    connection 'scm:git@github.com:TouK/sputnik.git'
                    developerConnection 'scm:git@github.com:Touk/sputnik.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'tomasz_kalkosinski'
                        name 'Tomasz Kalkosinski'
                    }
                    developer {
                        id 'marcin_cylke'
                        name 'Marcin Cylke'
                    }
                    developer {
                        id 'piotr_jagielski'
                        name 'Piotr Jagielski'
                    }
                    developer {
                        id 'karol_lassak'
                        name 'Karol Lassak'
                    }
                }
            }
        }
    }
}
