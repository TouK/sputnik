buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://repo.maven.apache.org/maven2" }
    }

    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:0.3.0'
        classpath 'pl.allegro.tech.build:axion-release-plugin:1.10.1'
    }
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'coveralls'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'pl.allegro.tech.build.axion-release'

scmVersion {
    tag {
        prefix = 'sputnik'
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

project.version = scmVersion.version

sourceCompatibility = 1.8
targetCompatibility = 1.8
group = 'pl.touk'
mainClassName = 'pl.touk.sputnik.Main'

run {
    // You can launch Sputnik having only build.gradle file in your repo and executing:
    // gradle run -Dexec.args="--conf example.properties --changeId 1234 --revisionId 4321"
    def arguments = System.getProperty("exec.args")
    if (arguments) {
        // Need to split the space-delimited value in the exec.args
        args arguments.split()
    }

    // change working directory and run sputnk from workspace
    def newWorkingDir = System.getProperty("exec.workingDir")
    if (newWorkingDir) {
        workingDir = newWorkingDir
    }
}

repositories {
    mavenCentral()
    maven { url "http://central.maven.org/maven2" }
    jcenter()
}

//noinspection GroovyAssignabilityCheck
dependencies {
    compile 'org.apache.httpcomponents:httpclient:4.3.1'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.3.0'
    compile 'com.intellij:annotations:12.0'
    compile 'org.slf4j:slf4j-api:1.7.26'
    compile 'org.slf4j:log4j-over-slf4j:1.7.26'
    compile 'ch.qos.logback:logback-classic:1.2.3'
    compile 'commons-cli:commons-cli:1.2'
    compile 'com.jayway.jsonpath:json-path:2.4.0'
    compile 'commons-io:commons-io:2.4'
    compile 'org.eclipse.jgit:org.eclipse.jgit:5.5.1.201910021850-r'

    compile 'com.urswolfer.gerrit.client.rest:gerrit-rest-java-client:0.8.8'

    // Checkstyle dependencies
    compile('com.puppycrawl.tools:checkstyle:8.1') {
        exclude group: 'com.google.guava'
    }

    // PMD dependencies
    compile('net.sourceforge.pmd:pmd-java:6.0.0') {
        exclude group: 'jaxen'
        exclude group: 'xerces'
        exclude group: 'junit'
        exclude group: 'org.apache.ant'
        exclude group: 'org.ow2.asm'
    }

    compile('jaxen:jaxen:1.1.6') { //1.1.6 in SpotBugs
        exclude group: 'xerces'
        exclude group: 'xalan'
        exclude group: 'com.ibm.icu'
    }
    compile('xerces:xercesImpl:2.9.1') {
        exclude group: 'xml-apis'
    }

    // SpotBugs dependencies
    compile('com.github.spotbugs:spotbugs:3.1.11') {
        exclude group: 'org.slf4j'
    }

	compile 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.9.0'

    // Scalastyle http://www.scalastyle.org/
    compile 'org.scalastyle:scalastyle_2.10:0.4.0'

    // CodeNarc http://codenarc.sourceforge.net/
    compile('org.codenarc:CodeNarc:1.4') {
        exclude module: 'groovy'
        exclude group: 'junit'
    }

    compile 'org.codehaus.groovy:groovy:2.3.4'

    // JSLint
    compile 'com.googlecode.jslint4java:jslint4java:2.0.5'

    // JSHint
    compile 'pl.touk:jshint4j:2.9.5'

    // github connector
    compile 'com.jcabi:jcabi-github:1.0'
    compile 'org.glassfish:javax.json:1.0.4'
    compile 'com.github.spullara.mustache.java:compiler:0.8.17'

    // external processes
    compile 'org.zeroturnaround:zt-exec:1.8'

    compile 'org.jetbrains.kotlin:kotlin-stdlib:1.3.31'

    // ktlint https://github.com/shyiko/ktlint
    compile 'com.github.shyiko.ktlint:ktlint-core:0.31.0'
    compile 'com.github.shyiko.ktlint:ktlint-ruleset-standard:0.31.0'

    // detekt
    compile 'io.gitlab.arturbosch.detekt:detekt-core:1.0.0-RC14'
    compile 'io.gitlab.arturbosch.detekt:detekt-rules:1.0.0-RC14'
    // dependency only for obtaining default configuration
    compile 'io.gitlab.arturbosch.detekt:detekt-cli:1.0.0-RC14'

    // transitive dependency that used non-SSL version of Maven Central
    // and version 1.74 that was not found
    compile 'com.beust:jcommander:1.78'

    compileOnly 'org.projectlombok:lombok:1.18.4'
    annotationProcessor 'org.projectlombok:lombok:1.18.4'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.4'
    testCompileOnly 'org.projectlombok:lombok:1.18.4'

    // Test dependencies
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.4.0"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:5.4.0"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.4.0"
    testImplementation 'org.junit.platform:junit-platform-launcher:1.1.0'
    testImplementation 'org.assertj:assertj-core:3.9.0'
    testImplementation 'org.mockito:mockito-core:2.23.4'
    testImplementation 'org.mockito:mockito-junit-jupiter:2.28.2'
    testImplementation('com.github.tomakehurst:wiremock:2.18.0') {
        exclude group: 'junit'
    }
}

test {
    useJUnitPlatform()
}

// Jacoco + coveralls
jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'javadoc'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

// quick way to copy all project dependencies (jar files) into one directory
// similar to dependency:copy-dependencies
task copyToLib(type: Copy) {
    into "$buildDir/lib"
    from configurations.runtime
}

artifacts {
    //Comment this archives jar due to duplicated jar task with Gradle 5
    //https://github.com/gradle/gradle/issues/8213
    //https://discuss.gradle.org/t/apply-plugin-signing-broke-in-gradle-5-1-1-and-prints-out-non-existent-task/31418
    //archives jar

    archives javadocJar
    archives sourcesJar
}

jar {
    manifest {
        attributes("Specification-Title": "Sputnik",
                "Specification-Version": scmVersion.version,
                "Implementation-Title": "Sputnik",
                "Implementation-Version": scmVersion.version)
    }
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            snapshotRepository(url: "https://philanthropist.touk.pl/nexus/content/repositories/snapshots//") {
                authentication(userName: snapshotUsername, password: snapshotPassword)
            }

            pom.project {
                name 'Sputnik'
                packaging 'jar'
                description 'Static code review for your Gerrit and Stash patchsets. Runs Checkstyle, PMD and SpotBugs for you!'
                url 'https://github.com/TouK/sputnik/'

                scm {
                    url 'scm:git@github.com:TouK/sputnik.git'
                    connection 'scm:git@github.com:TouK/sputnik.git'
                    developerConnection 'scm:git@github.com:Touk/sputnik.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'tomasz_kalkosinski'
                        name 'Tomasz Kalkosinski'
                    }
                    developer {
                        id 'marcin_cylke'
                        name 'Marcin Cylke'
                    }
                    developer {
                        id 'piotr_jagielski'
                        name 'Piotr Jagielski'
                    }
                    developer {
                        id 'karol_lassak'
                        name 'Karol Lassak'
                    }
                    developer {
                        id 'dominik_przybysz'
                        name 'Dominik Przybysz'
                    }
                    developer {
                        id 'henning_hoefer'
                        name 'Henning Hoefer'
                    }
                }
            }
        }
    }
}
